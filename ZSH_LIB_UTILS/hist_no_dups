#!/usr/bin/zsh
# Inline ansi
BOLD="\033[1m"
ITALIC="\033[3m"
RESET="\033[m"
REVERSE="\033[7m"
STRIKE="\033[9m"
UNDER="\033[4m"

BLUE_FG="\033[34m"
CYAN_FG="\033[36m"
GREEN_FG="\033[32m"
MAGENTA_FG="\033[35m"
RED_FG="\033[31m"
WHITE_FG="\033[37m"
YELLOW_FG="\033[33m"

# Declarations
typeset -A TIME_CMD
typeset -A TIME_ARG
typeset -a CMD_UNIQ
typeset -a HIST_NEW
typeset -A LOCAL_EXES
typeset -A SEEN

# Constants
#_HIST_FILE=./.zsh_history # For testing
_HIST_FILE=~/.zsh_history
_HIST_FILE_NEW=/tmp/zsh_history.new
_ALIAS_FILE=/etc/zsh/aliases
_LOCAL_EXE_PATH=~/Code/LOCAL
_LIB_DIR=/usr/local/lib
_CMD_CACHE=~/.cmd_cache


# Imports
_LIB_DIR=/usr/local/lib
source ${_LIB_DIR}/LIB_INIT.zsh # Must be first
source ${_LIB_DIR}/UTILS.zsh
source ${_LIB_DIR}/LIB_DEPS.zsh # Must be last

# Functions
validate_cmd () {
	local CMD=${@}

	[[ -z ${CMD} ]] && return 1

	[[ ${_DEBUG} -gt 0 ]] && dbg "${0}:${LINENO} ARGS:${@}"

	grep -q ${CMD} ${_CMD_CACHE} >/dev/null 2>&1
	[[ ${?} -eq 0 ]] && return 0

	# Validate command or alias
	which ${CMD} >/dev/null 2>&1
	if [[ ${?} -eq 0 ]];then
			echo ${CMD} >> ${_CMD_CACHE}
			return 0
	elif [[ ${LOCAL_EXES[${CMD}]} -eq 1 ]];then
			echo ${CMD} >> ${_CMD_CACHE}
			return 0
	else
		grep -q "^alias ${CMD}=" < ${_ALIAS_FILE} >/dev/null 2>&1
		if [[ ${?} -eq 0 ]];then
			echo ${CMD} >> ${_CMD_CACHE}
			return 0
		fi
	fi

	[[ ${_DEBUG} -gt 0 ]] && dbg "${0}:${LINENO} Invalid command:${CMD}"

	return 1
}

get_last_cmd () {
	local CMD=${@}
	local ITEM_NDX=0
	local -a ITEM_STACK
	local HIT=false
	local IN_ITEM=false
	local V

	[[ ${_DEBUG} -gt 0 ]] && dbg "${0}:${LINENO} ARGS:${@}"

	# Find most recent cmd
	for V in ${(onv)TIME_CMD};do
		if [[ ${CMD} == "${V/\#\#*}" ]];then
			HIT=true
			IN_ITEM=true
			((ITEM_NDX++))
			ITEM_STACK[${ITEM_NDX}]=${(k)TIME_CMD[(r)${V}]} # Unique timestamp resolution
			# Echo "CMD:${CMD} RLU:${(k)TIME_CMD[(r)${V}]} ITEM_NDX:${ITEM_NDX} ITEM_STACK:${ITEM_STACK[${ITEM_NDX}]}" >&2
		else
			IN_ITEM=false
		fi
		[[ ${HIT} == 'true' && ${IN_ITEM} == 'false' ]] && break
	done

	[[ ${_DEBUG} -gt 0 ]] && dbg "${0}:${LINENO} Most recent:${ITEM_STACK[${ITEM_NDX}]}"

	echo ${ITEM_STACK[${ITEM_NDX}]} # Return most recent instance based on timestamp
}

_cleanup () {
	[[ ${_DEBUG} -gt 0 ]] && dbg "${0}:${LINENO} ARGS:${@}"

	[[ -e ${_HIST_FILE_NEW} ]] && rm -f ${_HIST_FILE_NEW}
}

#--Begin GetOpts--
local -a OPTIONS
local OPTION
local DESC

read -r -d '' DESC<<EOF
This script does...
EOF

parse_opts () {
	local OPTS=${@}
	local -a OPTSTR
	local LETTER_OPT
	local O

	for O in {1..${#OPTS}};do
		[[ ${OPTS[${O}]} =~ '[a-z]' ]] && LETTER_OPT=${OPTS[${O}]}
		[[ ${O} -eq 1 && ${OPTS[${O}]} == ":" ]] && continue
		[[ ${O} -gt 1 && ${OPTS[${O}]} == ":" ]] && OPTSTR+=$(opt_type ${LETTER_OPT}) && continue
		OPTSTR+="-${OPTS[${O}]}"
	done
	echo ${OPTSTR}
}

opt_exists () {
	local OPT=${1}
	[[ $(( $OPTIONS[(Ie)${OPT}] )) -ne 0 ]] && return 0 || return 1
}

opt_type () {
	local OPT=${1}
	case ${OPT} in
		d) echo "<DIR>";;
		f) echo "<FILE>";;
		t) echo "<FILE_TYPE>";;
	esac
}

usage () {
	local -a PENDING_MSGS=(${@})
	local OPTS=$(parse_opts ${OPTSTR})

	[[ ${_SMCUP} == 'true' ]] && do_rmcup

	tabs 5
	(
	echo -e "\n${WHITE_FG}\nUsage${RESET}: ${_SCRIPT} ${WHITE_FG}[${RESET}${OPTS}${WHITE_FG}]${RESET}\n"
	echo -e "${WHITE_FG}-H${RESET} help"
	echo -e "${WHITE_FG}-D${RESET} debug (repeats increase level)"
	echo -e "\n${WHITE_FG}DESC${RESET}:${DESC}"
	) >&2

	exit_leave $(msg_exit E "${PENDING_MSGS}")
}

OPTSTR=":HD"
OPTIND=0

while getopts ${OPTSTR} OPTION;do
	case $OPTION in
     H) usage;;
     D) dbg_set_level;;
     :) print -u2 "${RED_FG}${_SCRIPT}${RESET}: option: -${OPTARG} requires an argument"; usage;;
    \?) print -u2 "${RED_FG}${_SCRIPT}${RESET}: unknown option -${OPTARG}"; usage;;
	esac
	[[ ${OPTION} != 'D' ]] && OPTIONS+=${OPTION}
done
shift $((OPTIND -1))
#--End GetOpts--
 
# Execution
sort -u -o ${_CMD_CACHE} < ${_CMD_CACHE}
sort -u -o ${_HIST_FILE} < ${_HIST_FILE}

local -a LIST=("${(f)$(find ${_LOCAL_EXE_PATH} -executable)}")

NDX=0
tput sc
for L in ${LIST};do
	((NDX++))
	# Tput el; printf "\rProcessing line ${WHITE_FG}%d${RESET} of ${WHITE_FG}%d${RESET} lines ${WHITE_FG}%%${BOLD}${GREEN_FG}%.2f${RESET}" ${NDX}. ${#LIST}. $(( NDX * 100. / ${#LIST}. ))
	if [[ $(file ${L}) =~ 'zsh script' ]];then
		LOCAL_EXES[${L:t}]=1
	fi
done
tput el1
tput rc

HIST_CNT=0
VALID_CNT=0
INVALID_CNT=0
DUPLICATE_CNT=0
NDX=0

NDX=0
HF_LINES=$(wc -l ${_HIST_FILE})

while read LINE;do
	((NDX++))
#	# Tput el; printf "\rProcessing line ${WHITE_FG}%d${RESET} of ${WHITE_FG}%d${RESET} lines ${WHITE_FG}%%${BOLD}${GREEN_FG}%.2f${RESET}" ${NDX}. ${HF_LINES}. $(( NDX * 100. / ${HF_LINES}. ))
	TIME=$(cut -d':' -f2 <<<${LINE} | sed 's/^ *//')
	CMD=$(cut -d':' -f3- <<<${LINE} | sed 's/0;//' | cut -d' ' -f1)
	ARG=$(cut -d':' -f3- <<<${LINE} | sed 's/0;//' | cut -s -d' ' -f2-)
	if validate_cmd ${CMD};then
		((VALID_CNT++))
		TIME_CMD[${TIME}]="${CMD}##${NDX}" # Force unique time stamp resolution
		TIME_ARG[${TIME}]="${ARG}"
		CMD_UNIQ+=${CMD}
		[[ ${_DEBUG} -gt 0 ]] && dbg "${0}:${LINENO} ${CYAN_FG}Unique${RESET}:${CMD}"
	else
		[[ ${_DEBUG} -gt 0 ]] && dbg "${0}:${LINENO} ${RED_FG}Eliminated${RESET}:${CMD}"
		((INVALID_CNT++))
	fi
	((HIST_CNT++))
done < ${_HIST_FILE}

HIST_NEW=()

NDX=0
for C in ${(o)CMD_UNIQ};do
	[[ ${C} =~ '^\[' ]] && continue # Skip bracket commands
	((NDX++))
	# Tput el; printf "\rProcessing line ${WHITE_FG}%d${RESET} of ${WHITE_FG}%d${RESET} lines ${WHITE_FG}%%${BOLD}${GREEN_FG}%.2f${RESET}" ${NDX} ${#CMD_UNIQ} $(( NDX * 100. / ${#CMD_UNIQ} ))
	TIME_STAMP=$(get_last_cmd ${C})
	CMD_ARGS=$(tr -d '[:space:]' <<<${C}${TIME_ARG[${TIME_STAMP}]}) # Cmd + args
	[[ -n ${CMD_ARGS} && ${SEEN[${CMD_ARGS:q}]} -ge 1 ]] && continue
	HIST_NEW+=": ${TIME_STAMP}:0;${C} ${TIME_ARG[${TIME_STAMP}]}"
	(( SEEN[${CMD_ARGS:q}]+=1 ))
	[[ ${_DEBUG} -gt 0 ]] && dbg "${0}:${LINENO} ${GREEN_FG}Added to history${RESET}: ${TIME_STAMP}:0;${C} ${TIME_ARG[${TIME_STAMP}]}"
done

DUPLICATE_CNT=$(( (HIST_CNT-${#HIST_NEW}) - INVALID_CNT ))
printf "History ${WHITE_FG}%d${RESET} | (${WHITE_FG}%d${RESET}) | ${WHITE_FG}%d${RESET} lines\n" ${HIST_CNT} $(( INVALID_CNT + DUPLICATE_CNT )) ${#HIST_NEW}

_cleanup

for L in ${(on)HIST_NEW};do
	echo ${L} >>${_HIST_FILE_NEW}
done

if [[ -e ${_HIST_FILE_NEW} ]];then
	cp ${_HIST_FILE} ${_HIST_FILE}.bak
	cp ${_HIST_FILE_NEW} ${_HIST_FILE}
fi

fc -R ${_HIST_FILE}

_cleanup

exit_leave
